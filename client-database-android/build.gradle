/* stocks is client-server program to manage a household's food stock
 * Copyright (C) 2019  The stocks developers
 *
 * This file is part of the stocks program suite.
 *
 * stocks is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * stocks is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.android.library'
    id 'checkstyle'
    id 'jacoco'
}

String customGeneratedSources = "generated/source/custom/"

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 31
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    sourceSets {
        main {
            java.srcDir "${buildDir}/${customGeneratedSources}"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            jniDebuggable true
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        checkAllWarnings true
        abortOnError false
        lintConfig file("$projectDir/../config/android-lint/lint.xml")
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled true
        unitTests {
            includeAndroidResources = true
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    implementation 'javax.inject:javax.inject:1'
    implementation "androidx.room:room-runtime:2.3.0"
    implementation 'androidx.room:room-rxjava3:2.3.0'
    implementation 'com.github.requery:sqlite-android:3.35.5'
    implementation project(':client-core')
    implementation 'io.reactivex.rxjava3:rxjava:3.1.3'
    implementation "com.google.auto.value:auto-value-annotations:1.8.2"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "com.google.dagger:dagger:2.40.4"

    annotationProcessor "com.google.auto.value:auto-value:1.8.2"
    annotationProcessor "androidx.room:room-compiler:2.3.0"
    annotationProcessor "com.google.dagger:dagger-compiler:2.40.4"

    testImplementation 'junit:junit:4.13'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:2.27.0'

    androidTestImplementation 'junit:junit:4.13'
    androidTestImplementation 'org.mockito:mockito-core:2.27.0'
    androidTestImplementation "androidx.room:room-testing:2.3.0"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'

    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:2.40.4"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}

task generateSqlSnippets(type: Exec) {
    outputs.dir("${buildDir}/${customGeneratedSources}")
    inputs.file('bin/generate_sql.sh')
    mkdir "${buildDir}/${customGeneratedSources}"
    commandLine('bin/generate_sql.sh', "${buildDir}/${customGeneratedSources}/de/njsm/stocks/client/db/Sql.java")
}

tasks.withType(JavaCompile) {
    dependsOn generateSqlSnippets
}

jacoco {
    toolVersion = '0.8.7'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug/classes")
    def mainSrc = "$projectDir/src/main/java"

    additionalSourceDirs.from = files(mainSrc)
    sourceDirectories.from = files([mainSrc])
    classDirectories.from = files([debugTree])

    executionData.from = files([
            "$buildDir/jacoco/testDebugUnitTest.exec",
            fileTree(dir: "$buildDir/outputs/code_coverage/debugAndroidTest/connected/", includes: ["**/*.ec"])
    ])
}

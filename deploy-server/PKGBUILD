# Maintainer: aliena <dairyman@njsm.de>
pkgname=stocks-server
pkgver=0.5.7
pkgrel=2
pkgdesc="Manages a household's stocks"
arch=('any')
url="https://github.com/F1rst-Unicorn/stocks"
license=('MIT')
depends=('jre8-openjdk'
         'java-environment'
         'nginx'
         'openssl'
         'mariadb'
         'tomcat8')
makedepends=('git'
             'maven')
__branch=${STOCKSBRANCH:-master}
source=("git://www.github.com/F1rst-Unicorn/stocks#branch=$__branch")
install=stocks-server.install
sha512sums=('SKIP')
backup=("etc/$pkgname/stocks.properties"
	"usr/lib/$pkgname/setup-ca")
		
prepare() {
    # Verify that the correct PKGBUILD is used to prevent version conflicts
    cd $srcdir/stocks/deploy-server
    
    OUTPUT=$(grep "pkgver=$pkgver" PKGBUILD || true)
    
    if [ -z $OUTPUT ]; then
        echo "Cloned PKGBUILD has different version"
        echo "Please use the same PKGBUILD for building as the branch actually has"
        exit 1
    fi
    OUTPUT=$(grep "pkgrel=$pkgrel" PKGBUILD || true)
    
    if [ -z $OUTPUT ]; then
        echo "Cloned PKGBUILD has different release"
        echo "Please use the same PKGBUILD for building as the branch actually has"
        exit 1
    fi  
}

build() {
    cd "$srcdir/stocks"
    
    cd server
    mvn -Dmaven.test.skip=true clean package
    cd ..

    cd sentry
    mvn -Dmaven.test.skip=true clean package
    cd ..
}

package() {
    cd "$srcdir/stocks"
    
    install -D -m 444 deploy-server/schema.sql               $pkgdir/usr/share/$pkgname/schema.sql
    install -D -m 444 deploy-server/nginx.conf               $pkgdir/usr/share/$pkgname/nginx.example.conf
    install -D -m 444 deploy-server/tomcat-server.xml        $pkgdir/usr/share/$pkgname/server.xml
    install -D -m 444 deploy-server/openssl.cnf              $pkgdir/usr/share/$pkgname/openssl.cnf
    install -D -m 444 deploy-server/openssl_intermediate.cnf $pkgdir/usr/share/$pkgname/openssl_intermediate.cnf
    
    mkdir -p $pkgdir/usr/share/$pkgname/root/CA/intermediate
    mkdir -p $pkgdir/usr/share/$pkgname/root/nginx
    
    install -D -m 555 deploy-server/setup-ca     $pkgdir/usr/lib/$pkgname/setup-ca
    install -D -m 555 deploy-server/nginx-reload $pkgdir/usr/lib/$pkgname/nginx-reload
    install -D -m 444 server/target/server-*.war $pkgdir/usr/lib/$pkgname/server.war
    install -D -m 444 sentry/target/sentry-*.war $pkgdir/usr/lib/$pkgname/sentry.war
    
    install -D -m 644 deploy-server/stocks.properties $pkgdir/etc/$pkgname/stocks.properties
    
    install -D -m 444 README.md $pkgdir/usr/share/doc/$pkgname/README.md
    install -D -m 444 manual/server-admin/server-installation.md $pkgdir/usr/share/doc/$pkgname/
    install -D -m 444 manual/server-admin/maintainance.md $pkgdir/usr/share/doc/$pkgname/
    
    install -D -m 444 LICENSE.md $pkgdir/usr/share/licenses/$pkgname/LICENSE.md
    
    mkdir -p $pkgdir/var/log/$pkgname
    touch $pkgdir/var/log/$pkgname/stocks.log
}

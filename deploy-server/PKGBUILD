# Maintainer: aliena <dairyman@njsm.de>
pkgname=stocks-server
pkgver=2.2.0.0
pkgrel=1
pkgdesc="Manages a household's stocks"
arch=('any')
url="https://github.com/F1rst-Unicorn/stocks"
license=('MIT')
depends=('jre8-openjdk'
         'java-environment'
         'nginx'
         'openssl'
         'tomcat8')
makedepends=('git'
             'maven')
source=("http://www.do.not.use.com/stocks.tar")
install=stocks-server.install
sha512sums=('SKIP')
backup=("etc/$pkgname/stocks.properties"
	"usr/lib/$pkgname/setup-ca")

build() {
    cd "$srcdir"

    if [[ "$CI_SERVER" != "" ]] ; then
        MVN_ARGS="-P teamcity"
    fi

    cd server
    mvn $MVN_ARGS -Dmaven.test.skip=true clean package
    cd ..

    cd deploy-server/postgresql-migration
    mvn $MVN_ARGS -Dmaven.test.skip=true clean package
    cd ..
}

package() {
    cd "$srcdir"

    install -D -m 444 deploy-server/config/schema.sql                   $pkgdir/usr/share/$pkgname/schema.sql
    install -D -m 444 deploy-server/config/nginx.conf                   $pkgdir/usr/share/$pkgname/nginx.example.conf
    install -D -m 444 deploy-server/config/tomcat-server.xml            $pkgdir/usr/share/$pkgname/server.xml
    install -D -m 444 deploy-server/config/openssl.cnf                  $pkgdir/usr/share/$pkgname/openssl.cnf
    install -D -m 444 deploy-server/config/openssl_intermediate.cnf     $pkgdir/usr/share/$pkgname/openssl_intermediate.cnf

    mkdir -p $pkgdir/usr/share/$pkgname/root

    install -D -m 555 deploy-server/config/setup-ca     $pkgdir/usr/lib/$pkgname/setup-ca
    install -D -m 555 deploy-server/config/nginx-reload $pkgdir/usr/lib/$pkgname/nginx-reload
    install -D -m 555 deploy-server/config/migrate-postgres.sh $pkgdir/usr/lib/$pkgname/migrate-to-postgres
    install -D -m 444 server/target/server-*.war        $pkgdir/usr/lib/$pkgname/server.war
    install -D -m 444 deploy-server/postgresql-migration/target/postgresql-migration-1.0.jar        $pkgdir/usr/lib/stocks-server/migrate-postgres.jar


    install -D -m 644 deploy-server/config/stocks.properties $pkgdir/etc/$pkgname/stocks.properties

    install -D -m 444 README.md                                     $pkgdir/usr/share/doc/$pkgname/README.md
    install -D -m 444 manual/server-admin/server-installation.md    $pkgdir/usr/share/doc/$pkgname/
    install -D -m 444 manual/server-admin/maintainance.md           $pkgdir/usr/share/doc/$pkgname/

    install -D -m 444 LICENSE.md                             $pkgdir/usr/share/licenses/$pkgname/LICENSE.md

    mkdir -p $pkgdir/var/log/$pkgname
}
